subprojects {
    version = '1.0-master'

    apply plugin: 'scala'

    repositories {
        mavenCentral()
        maven { url 'https://raw.github.com/Blackrush/photon-persist/maven-repo' }
        maven { url 'https://raw.github.com/Blackrush/flatjson/maven-repo' }
    }

    dependencies {
		compile "org.scala-lang:scala-library:${scalaStdlibVersion}"
		compile "com.typesafe:scalalogging-slf4j_${scalaVersion}:${typesafeLoggingVersion}"
		compile "com.twitter:util-core_${scalaVersion}:${twitterUtilVersion}"

		testCompile "org.scalatest:scalatest_${scalaVersion}:${scalatestVersion}"

		runtime "org.scala-lang:scala-compiler:${scalaStdlibVersion}"
    }

    buildscript {
        repositories {
            mavenCentral()
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['must_not_use'] // enable Zinc joint compilation
            scala.srcDirs = ['main/src']
            resources.srcDirs = ['main/res']
        }
        test {
			java.srcDirs = ['must_not_use']
            scala.srcDirs = ['test/src']
			resources.srcDirs = ['test/res']
        }
    }

    compileScala {
        scalaCompileOptions.useAnt = false // use Zinc instead of Ant
        scalaCompileOptions.additionalParameters = ['-feature']
    }

    test << { // scalatest support
        ant.taskdef(name: 'scalatest', classname: 'org.scalatest.tools.ScalaTestAntTask', classpath: classpath.asPath)
        ant.scalatest(runpath: testClassesDir, haltonfailure: 'true', fork: 'false') {
            reporter(type: 'stderr')
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}
